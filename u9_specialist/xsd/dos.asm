#include "defs.inc"

	ORG 0D000H

ENTRY:
	lxi	h,BUF
	lxi	b,2048-256
clrmem:
	mvi	m,0
	inx	h
	dcx	b
	mov	a,c
	ora	b
	jnz	clrmem
	CALL FS_RST
LOOP:	LXI SP,0F6CFH
	LXI H,A_DRIVE
	CALL PRINT
	LXI H,CWD
	CALL PRINT
	MVI C,'>'
	CALL PUTC
	CALL READLN
	LXI H,LOOP
	PUSH H
	LXI H,CMDLST
LOOP1:	MOV A,M
	ORA A
	JZ RUNFILE
	LXI D,LINEBUF
LOOP2:	MOV A,M
	ORA A
	JZ LOOP3
	LDAX D
	CMP M
	JNZ LOOP4
	INX D
	INX H
	JMP LOOP2
LOOP3:	LDAX D
	ORA A
	JZ LOOP5
	CPI 20H
	JZ LOOP5
LOOP4:	MOV A,M
	INX H
	ORA A
	JNZ LOOP4
	INX H
	INX H
	JMP LOOP1
LOOP5:	INX H
	MOV E,M
	INX H
	MOV D,M
	XCHG
	PCHL

PRINTN:	MOV C,M
	INX H
	CALL PUTC
	DCR B
	JNZ PRINTN
	RET

C_DIR:	LXI H,NEWLINE
	CALL PRINT
	LXI H,LINEBUF+2
	INX H
	MOV A,M
	CPI 20H
	JZ $-4
	ORA A
	JNZ $+6
	LXI H,ALLFILS
	XCHG
	CALL FS_FNDF
	JC C_DIRNO
C_DIR1:	PUSH H
	LXI B,11
	DAD B
	MOV A,M
	POP H
	ANI 8
	JNZ C_DIR2
	MVI B,8
	CALL PRINTN
	MVI C,' '
	CALL PUTC
	MVI B,3
	CALL PRINTN
	LXI H,NEWLINE
	CALL PRINT
C_DIR2:	CALL FS_FNDN
	JNC C_DIR1
	RET
C_DIRNO:LXI H,NOFILES
	JMP PRINT

C_CD:	LXI H,LINEBUF+1
	INX H
	MOV A,M
	CPI 20H
	JZ $-4
	ORA A
	JNZ C_CD1
	LXI H,NEWLINE
	CALL PRINT
	LXI H,CWD
	JMP PRINT
C_CD1:	XCHG
	CALL FS_CHDIR
	RNC
	LXI H,NODIR
	JMP PRINT

RUNFILE:LXI H,NEWLINE
	CALL PRINT
	LXI H,LINEBUF
	MVI C,'.'
	CALL STRCHR
	JNC RUNF1
	LXI D,DOTRK
	CALL STRCPY
RUNF1:	LXI D,LINEBUF
	CALL F_OPEN
	LXI H,NOTFND
	JC PRINT
	LXI B,4
	LXI H,LINEBUF
	CALL F_READ
	LXI H,LINEBUF
	MOV A,M
	CPI 0E6H
	JNZ RUNF2
	LXI B,1
	LXI H,LINEBUF+4
	CALL F_READ
	LXI H,LINEBUF+1
RUNF2:
;	MOV D,M
	MOV e,M
	INX H
;	MOV E,M
	MOV d,M
	INX H
;	MOV B,M
	MOV c,M
	INX H
	MOV A,M
	SUB E
;	MOV C,A
	MOV b,A
;	MOV A,B
	MOV A,c
	SBB D
;	MOV B,A
	MOV c,A
	INX B
	XCHG
	PUSH H
	JMP F_READ

READLN:	LXI H,LINEBUF
READL1:	CALL GETC
	CPI 8
	JZ READL3
;	CPI 7FH
	CPI 5FH
	JZ READL3
	CPI 18H
	JZ READL4
	MVI M,0
	CPI 0DH
	RZ
	MOV M,A
	INX H
READL2:	MOV C,A
	CALL PUTC
	JMP READL1
READL3:	MOV A,L
	CPI LINEBUF & 255
	JZ READL1
	MVI C,8
	CALL PUTC
	MVI C,20H
	CALL PUTC
	MVI C,8
	CALL PUTC
	DCX H
	JMP READL1
READL4:	MOV A,M
	ORA A
	JZ READL1
	CPI 0DH
	JZ READL1
	INX H
	JMP READL2

NEWLINE:DB 0DH,0AH,0
A_DRIVE:DB 0DH,0AH,"A:",0
ALLFILS:DB "*",0
NOFILES:DB "NO FILE(S)",0
NODIR:	DB 0DH,0AH,"NO DIRECTORY",0
NOTFND:	DB "FILE NOT FOUND",0
DOTRK:	DB ".RKS",0

CMDLST:	DB "DIR",0
	DW C_DIR
	DB "CD",0
	DW C_CD
	DB 0

#include "sd_proc.inc"
#include "fs_proc.inc"

	END
